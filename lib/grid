#!/usr/bin/perl -w

# Grid reference calculation.  2-clause BSD license.

# Copyright 2015 /u/molo1134. All rights reserved.

use strict;
use utf8;
use Encode qw(decode);
use feature 'unicode_strings';
use Math::Trig;
use Math::Trig 'great_circle_distance';
use Math::Trig 'great_circle_bearing';
use URI::Escape;
binmode(STDOUT, ":utf8");

my $username = $ENV{'USER'} || $ENV{'USERNAME'} || getpwuid($<);

# change this to 0 to always exit with success
my $exitnonzeroonerror = 1;
$exitnonzeroonerror = 0 if $username eq 'eggdrop';

# change this to set the preferred method of terminal highlighting
my $highlight="vt100";
$highlight = "none" if ! -t STDOUT;
$highlight = "irc" if $username eq 'eggdrop';

our $mylat = undef;
our $mylon = undef;
my $de = undef;

my $loginfile = $ENV{'HOME'} . "/.qrzlogin";
if (-e ($loginfile)) {
  require($loginfile);
}

@ARGV = map { decode "utf-8", $_ } @ARGV;

if ($#ARGV < 0 || length($ARGV[0]) == 0) {
  if ($username eq 'eggdrop') {
    print "usage: !grid <grid>|<lat>,<lon>|<qth> [de <grid>|<lat>,<lon>|<qth>]\n";
  } else {
    print "usage: $0 <grid>|<lat>,<lon>|<qth> [de <grid>|<lat>,<lon>|<qth>]\n";
  }
  exit $exitnonzeroonerror;
}

my $query = undef;
my $type = undef;
my $raw = 0;

# eggdrop doesn't split args on spaces
@ARGV=split(' ', join(' ',@ARGV));

my $i = 0;
#while ($i <= $#ARGV) {
#  print "$i: $ARGV[$i++]\n";
#}
#$i = 0;

while ($i <= $#ARGV) {
  if ($ARGV[$i] =~ /^--raw$/i) {
    $raw = 1;
    $i++;
    next;
  }
  if (!defined($de) and $ARGV[$i] =~ /^(--geo|de|from)$/i) {
    $i++;
    $de = $ARGV[$i];
    $i++;
    next;
  }
  if (defined($de) and $ARGV[$i] =~ /^--geo$/) {
    # in case we have "de XXX --geo abc,xyz"
    last;
  }
  if (defined($de)) {
    $de = $de . " " . $ARGV[$i];
    $i++;
    next;
  }
  if (defined($query)) {
    $query = $query ." ". $ARGV[$i];
  } else {
    $query = $ARGV[$i];
  }
  $i++;
}

if (defined($de)) {
  $de = argToCoords($de);
  ($mylat, $mylon) = split(',', $de);
}

my ($lat, $lon) = split(',', argToCoords($query));

if ($raw == 1) {
  print "$lat,$lon\n";
  exit 0;
}

my $bold = "";
my $unbold = "";

if ($highlight eq "vt100") {
  $bold = "\e[1m";
  $unbold = "\e[0m";
} elsif ($highlight eq "irc" ) {
  $bold = "\002";
  $unbold = "\002";
}

my $grid = coordToGrid($lat, $lon);
my $placename = geolocate($lat, $lon);

print "$bold$grid$unbold: $lat, $lon";
if (defined($placename)) {
  print ": $bold$placename$unbold";
}

if (defined($mylat)) {

  # convert to radians
  my @origin = NESW($mylon, $mylat);
  my @foreign = NESW($lon, $lat);
  my ($dist, $bearing);

  # disable "experimental" warning on smart match operator use
  no if $] >= 5.018, warnings => "experimental::smartmatch";

  if (@origin ~~ @foreign) {	  # smart match operator - equality comparison
    $dist = 0;
    $bearing = 0;
  } else {
    $dist = great_circle_distance(@origin, @foreign, 6378.1);
    $bearing = rad2deg(great_circle_bearing(@origin, @foreign));
  }
  my $localgrid = coordToGrid($mylat, $mylon);

  printf(" -- %.1f km, %.0fÂ° from %s", $dist, $bearing, $localgrid);
}
print "\n";

exit 0;

# Notice the 90 - latitude: phi zero is at the North Pole.
# Example: my @London = NESW( -0.5, 51.3); # (51.3N 0.5W)
# Example: my @Tokyo  = NESW(139.8, 35.7); # (35.7N 139.8E)
sub NESW {
  deg2rad($_[0]), deg2rad(90 - $_[1])
}

sub gridToCoord {
  my $gridstr = shift;

  if (not $gridstr =~ /^[A-R]{2}[0-9]{2}([A-X]{2})?/i ) {
    print "\ninvalid grid\n";
    return undef;
  }

  my @grid = split (//, uc($gridstr));

  if ($#grid < 3) {
    return undef;
  }

  my $lat;
  my $lon;
  my $formatter;

  $lon = ((ord($grid[0]) - ord('A')) * 20) - 180;
  $lat = ((ord($grid[1]) - ord('A')) * 10) - 90;
  $lon += ((ord($grid[2]) - ord('0')) * 2);
  $lat += ((ord($grid[3]) - ord('0')) * 1);

  if ($#grid >= 5) {
    $lon += ((ord($grid[4])) - ord('A')) * (5/60);
    $lat += ((ord($grid[5])) - ord('A')) * (5/120);
    # move to center of subsquare
    $lon += (5/120);
    $lat += (5/240);
    # not too precise
    $formatter = "%.4f";
  } else {
    # move to center of square
    $lon += 1;
    $lat += 0.5;
    # even less precise
    $formatter = "%.1f";
  }

  # not too precise
  $lat = sprintf($formatter, $lat);
  $lon = sprintf($formatter, $lon);

  return join(',', $lat, $lon);
}

sub coordToGrid {
  my $lat = shift;
  my $lon = shift;
  my $grid = "";

  $lon = $lon + 180;
  $lat = $lat + 90;

  $grid .= chr(ord('A') + int($lon / 20));
  $grid .= chr(ord('A') + int($lat / 10));
  $grid .= chr(ord('0') + int(($lon % 20)/2));
  $grid .= chr(ord('0') + int(($lat % 10)/1));
  $grid .= chr(ord('a') + int(($lon - (int($lon/2)*2)) / (5/60)));
  $grid .= chr(ord('a') + int(($lat - (int($lat/1)*1)) / (2.5/60)));

  return $grid;
}

sub qthToCoords {
  my $place = uri_escape_utf8(shift);
  my $lat = undef;
  my $lon = undef;
  my $url = "http://maps.googleapis.com/maps/api/geocode/xml?address=$place&sensor=false";

  my $tries = 0;

  RESTART:
  $tries++;

  open (HTTP, '-|', "curl -k -s '$url'");
  binmode(HTTP, ":utf8");
  GET: while (<HTTP>) {
    #print;
    chomp;
    if (/OVER_QUERY_LIMIT/) {
      print "warning: over query limit\n" unless $raw == 1;
      close(HTTP);
      exit $exitnonzeroonerror if $tries > 3;
      goto RESTART;
    }
    if (/<lat>([+-]?\d+.\d+)<\/lat>/) {
      $lat = $1;
    }
    if (/<lng>([+-]?\d+.\d+)<\/lng>/) {
      $lon = $1;
    }
    if (defined($lat) and defined($lon)) {
      last GET;
    }
  }
  close HTTP;

  if (defined($lat) and defined($lon)) {
    return "$lat,$lon";
  } else {
    return undef;
  }
}

sub geolocate {
  my $lat = shift;
  my $lon = shift;

  my $url = "http://maps.googleapis.com/maps/api/geocode/xml?latlng=$lat,$lon&sensor=false";

  my $newResult = 0;
  my $getnextaddr = 0;
  my $addr = undef;
  my $type = undef;

  my %results;
  my $tries = 0;

  RESTART:

  open (HTTP, '-|', "curl -k -s '$url'");
  binmode(HTTP, ":utf8");
  while (<HTTP>) {
		#print;
    chomp;

    if (/OVER_QUERY_LIMIT/) {
      print "warning: over query limit\n" unless $raw == 1;
      close(HTTP);
      exit $exitnonzeroonerror if $tries > 3;
      goto RESTART;
    }

    if (/<result>/) {
      $newResult = 1;
      next;
    }

    if ($newResult == 1 and /<type>([^<]+)</) {
      $type = $1;
      $getnextaddr = 1;
      $newResult = 0;
      next;
    }

    if ($getnextaddr == 1 and /<formatted_address>([^<]+)</) {
      #print "$type => $1\n";
      $results{$type} = $1;
      $getnextaddr = 0;
      next;
    }
  }
  close HTTP;

  if (defined($results{"neighborhood"})) {
    $addr = $results{"neighborhood"};
  } elsif (defined($results{"locality"})) {
    $addr = $results{"locality"};
  } elsif (defined($results{"administrative_area_level_3"})) {
    $addr = $results{"administrative_area_level_3"};
  } elsif (defined($results{"postal_town"})) {
    $addr = $results{"postal_town"};
  } elsif (defined($results{"political"})) {
    $addr = $results{"political"};
  } elsif (defined($results{"postal_code"})) {
    $addr = $results{"postal_code"};
  } elsif (defined($results{"administrative_area_level_2"})) {
    $addr = $results{"administrative_area_level_2"};
  } elsif (defined($results{"administrative_area_level_1"})) {
    $addr = $results{"administrative_area_level_1"};
  } elsif (defined($results{"country"})) {
    $addr = $results{"country"};
  } elsif (defined($results{"sublocality"})) {
    $addr = $results{"sublocality"};
  } elsif (defined($results{"sublocality_level_3"})) {
    $addr = $results{"sublocality_level_3"};
  } elsif (defined($results{"sublocality_level_4"})) {
    $addr = $results{"sublocality_level_4"};
  }

  return $addr;
}

sub argToCoords {
  my $arg = shift;
  my $type;

  if ($arg =~ /^(grid:)? ?([A-R]{2}[0-9]{2}([a-x]{2})?)/i) {
    $arg = $2;
    $type = "grid";
  } elsif ($arg =~ /^(geo:)? ?([-+]?\d+(.\d+)?,\s?[-+]?\d+(.\d+)?)/i) {
    $arg = $2;
    $type = "geo";
  } else {
    $type = "qth";
  }

  my $lat = undef;
  my $lon = undef;
  my $grid = undef;

  if ($type eq "grid") {
    $grid = $arg;
  } elsif ($type eq "geo") {
    ($lat, $lon) = split(',', $arg);
  } elsif ($type eq "qth") {
    my $ret = qthToCoords($arg);
    if (!defined($ret)) {
      print "'$arg' not found.\n";
      exit $exitnonzeroonerror;
    }
    ($lat, $lon) = split(',', $ret);
  }

  if (defined($grid)) {
    ($lat, $lon) = split(',', gridToCoord(uc($grid)));
  } 

  return join(',', $lat, $lon);
}

# vim: set ts=8 sw=2 sts=2

#!/usr/bin/perl -w
# long-term solar activity forecasts from NOAA and NASA
#
# 2-clause BSD license.
# Copyright (c) 2019, 2020 molo1134@github. All rights reserved.

# source 1: NOAA SWPC Predicted Solar Cycle
#   * https://www.swpc.noaa.gov/products/solar-cycle-progression

# source 2: NASA Marshall Solar Cycle Forecast
#   * https://www.nasa.gov/solar-cycle-progression-and-forecast/

# source 3: ITU HFBC:
#   * https://www.itu.int/ITU-R/eTerrestrial/eBroadcasting/eHFBC/api/hfbc/reftables/tables/sunspots
#   * https://www.itu.int/itu-r/eterrestrial/ebroadcasting/ehfbc#/reftables/tables/RefTableOverview

use strict;
use utf8;
use feature 'unicode_strings';
binmode(STDOUT, ":utf8");

use JSON qw( decode_json );
use POSIX qw(strftime);

use File::Basename;
use Cwd 'realpath';
use lib dirname(realpath(__FILE__));
use Colors;

my ($datestamp, $lastmonth, $firstmonth, $firstyear);
my (%sfi, %sn);
my $maxsfi = -1.0;
my $minsfi = 10000000.0;
my $maxsn = -1.0;
my $minsn = 10000.0;
my $count = 0;

my $url = "https://services.swpc.noaa.gov/json/solar-cycle/predicted-solar-cycle.json";

open (JSON, '-|', "curl --max-time 10 -L -k -s $url");
binmode(JSON, ":utf8");
my $json = do { local $/; <JSON> };  # read entire file
close(JSON);
my $j = decode_json($json) or die "parse error: $json\n";

foreach my $rec (@{$j}) {
  #print $rec->{'time-tag'}, "\n";
  #print $rec->{'predicted_ssn'}, "\n";
  #print $rec->{'predicted_f10.7'}, "\n";
  my ($y, $m) = split '-', $rec->{'time-tag'};
  $firstyear = $y if not defined $firstyear;
  next if $y > $firstyear + 3; # truncate the forecast to 3 years
  my $spots = $rec->{'predicted_ssn'};
  my $flux = $rec->{'predicted_f10.7'};
  $count++;
  if ($spots ne "-1") {
    $sn{"$y-$m"} = $spots;
    $maxsn = $spots if $spots > $maxsn;
    $minsn = $spots if $spots < $minsn;
  }
  if ($flux ne "-1") {
    $sfi{"$y-$m"} = $flux;
    $maxsfi = $flux if $flux > $maxsfi;
    $minsfi = $flux if $flux < $minsfi;
  }
  $firstmonth = "$y-$m" if not defined $firstmonth;
  $lastmonth = "$y-$m";
}

#print "NOAA Monthly forecast as of: $datestamp; $firstmonth to $lastmonth\n";
#print "NOAA Monthly forecast: $firstmonth to $lastmonth\n";
#print "Max SFI: $maxsfi; Min SFI: $minsfi\n";
#print "Max SN $maxsn; Min SN $minsn\n";

printf "NOAA:%s\n", yearMarkers(sort keys %sfi);
printf "SFI: %s : $minsfi-$maxsfi\n", join("", sfiToColorBlocks($minsfi, $maxsfi, \%sfi));
printf "SN:  %s : $minsn-$maxsn\n", join("", snToColorBlocks($minsn, $maxsn, \%sn));

my $found = 0;
my $time = time();
my %ap;
$maxsfi = -1.0;
$minsfi = 10000000.0;
my $maxap = -1.0;
my $minap = 10000.0;

while (not $found) {

  my $currmonth = lc strftime("%b%Y", gmtime $time);
  my $curr_y_m = strftime("%Y/%m", gmtime $time);
  $url = "https://www.nasa.gov/wp-content/uploads/${curr_y_m}/${currmonth}f10-prd.txt";
  %sfi = ();
  $count = 0;
  $firstmonth = undef;
  $firstyear = undef;

  #  print"$url\n";

  open (HTTP, '-|', "curl --max-time 10 -L -k -s -f $url");
  binmode(HTTP, ":utf8");
  while (<HTTP>) {
    if (/^\s*20/) {
      my (undef, $y, $m, undef, $flux, undef, undef, $apindex) = split(/\s+/);
      $count++;
      $found = 1;

      $y =~ s/\.[0-9]+$//;
      $firstyear = $y if not defined $firstyear;
      next if $y > $firstyear + 3; # truncate the forecast to 3 years
      $m = monToNum($m);
      $firstmonth = "$y-$m" if not defined $firstmonth;
      $lastmonth = "$y-$m";

      $sfi{"$y-$m"} = $flux;
      $maxsfi = $flux if $flux > $maxsfi;
      $minsfi = $flux if $flux < $minsfi;

      $ap{"$y-$m"} = $apindex;
      $maxap = $apindex if $apindex > $maxap;
      $minap = $apindex if $apindex < $minap;
    }
  }
  close HTTP;

  $time = $time - (60*60*24*30) if not $found;
}

#print "NASA Monthly forecast as of: $currmonth; $firstmonth to $lastmonth\n";

if ($found) {
  printf "NASA:%s\n", yearMarkers(sort keys %sfi);
  printf "SFI: %s : $minsfi-$maxsfi\n", join("", sfiToColorBlocks($minsfi, $maxsfi, \%sfi));
  printf "AP:  %s : $minap-$maxap\n", join("", apToColorBlocks($minap, $maxap, \%ap));
}

$url = "https://www.itu.int/ITU-R/eTerrestrial/eBroadcasting/eHFBC/api/hfbc/reftables/tables/sunspots";
open (JSON, '-|', "curl --max-time 10 -L -k -s $url");
binmode(JSON, ":utf8");
$json = do { local $/; <JSON> };  # read entire file
close(JSON);
$j = decode_json($json) or die "parse error: $json\n";
#print "$json\n";

$maxsn = -1.0;
$minsn = 10000.0;
%sn = ();
$count = $found = 0;
my $thisyear = strftime("%Y", gmtime time);
($datestamp, $lastmonth, $firstmonth, $firstyear) = (undef, undef, undef, undef);
#print join(" ", keys %{$j->{data}}), "\n";
foreach my $rec (@{$j->{data}->{sunspots}}) {
  #print "$rec: $rec->{year}, $rec->{month}, $rec->{sunspotNumber}\n";
  my ($y, $m) = ($rec->{year}, $rec->{month});
  $m = sprintf("%02u", $m);
  my $spots = $rec->{sunspotNumber};
  next if $y < $thisyear;
  $maxsn = $spots if $spots > $maxsn;
  $minsn = $spots if $spots < $minsn;
  $sn{"$y-$m"} = $spots;
  $count++;
  $firstmonth = "$y-$m" if not defined $firstmonth;
  $lastmonth = "$y-$m";
}
printf "ITU :%s\n", yearMarkers(sort keys %sn);
printf "SN:  %s : $minsn-$maxsn\n", join("", snToColorBlocks($minsn, $maxsn, \%sn));



sub sfiToColorBlocks {
  my $minsfi = shift;
  my $maxsfi = shift;
  my $hashref = shift;
  my %sfi = %$hashref;

  my @result;

  # linear scale from 75 to 235, with 16 steps
  my $minscale = 75;
  my $maxscale = 235;
  my $steps = 16;
  my @chars = split(//, "▁▁▂▂▃▃▄▄▅▅▆▆▇▇██↑↑");

  foreach my $k (sort(keys(%sfi))) {
    my $v = $sfi{$k};

    #print "$k => $v\n";

    # year boundary
    push @result,"|" if ($k =~ /-01$/);

    my $chr;

    my $rescale = $v - $minscale;
    my $index;
    if ($v < $minscale) {
      $index = 0;
    } else {
      $index = int($rescale / (($maxscale-$minscale)/$steps));
    }

    if ($index < 1) {
      $chr = darkRed($chars[int($index)]);
    } elsif ($index < 2) {
      $chr = red($chars[int($index)]);
    } elsif ($index < 3) {
      $chr = red($chars[int($index)]);
    } elsif ($index < 4) {
      $chr = darkYellow($chars[int($index)]);
    } elsif ($index < 5) {
      $chr = darkYellow($chars[int($index)]);
    } elsif ($index < 6) {
      $chr = yellow($chars[int($index)]);
    } elsif ($index < 7) {
      $chr = yellow($chars[int($index)]);
    } elsif ($index < 8) {
      $chr = green($chars[int($index)]);
    } elsif ($index < 9) {
      $chr = green($chars[int($index)]);
    } elsif ($index < 10) {
      $chr = lightgreen($chars[int($index)]);
    } elsif ($index < 11) {
      $chr = lightgreen($chars[int($index)]);
    } elsif ($index < 12) {
      $chr = lightblue($chars[int($index)]);
    } elsif ($index < 13) {
      $chr = lightblue($chars[int($index)]);
    } elsif ($index < 14) {
      $chr = cyan($chars[int($index)]);
    } elsif ($index < 15) {
      $chr = cyan($chars[int($index)]);
    } elsif ($index < 16) {
      $chr = lightcyan($chars[int($index)]);
    } else {
      $chr = magenta("↑");
    }
    push @result,$chr;
  }
  return @result;
}

# SN   < 16: dark red
# SN  17-36: red
# SN  37-64 yellow
# SN  > 65 green
sub snToColorBlocks {

  my $minsn = shift;
  my $maxsn = shift;
  my $hashref = shift;
  my %sn = %$hashref;

  my $chr = undef;
  my @result;

  foreach my $k (sort(keys(%sn))) {
    my $v = $sn{$k};

    $chr = valToBlock($v, $minsn, $maxsn);

    # year boundary
    push @result,"|" if ($k =~ /-01$/);

    if ($v < 16.0) {
      push @result, darkRed($chr);
    } elsif ($v < 37.0) {
      push @result, red($chr);
    } elsif ($v < 65.0) {
      push @result, yellow($chr);
    } else {
      push @result, green($chr);
    }
  }
  return @result;
}

#   colors from: http://www.swpc.noaa.gov/products/station-k-and-indices
# Aindex  < 20: green
# Aindex 20-29: yellow
# Aindex 30-49: red
# Aindex 50-99: dark red
# Aindex > 100: light blue
sub apToColorBlocks {

  my $minsfi = shift;
  my $maxsfi = shift;
  my $hashref = shift;
  my %sfi = %$hashref;

  my $chr = undef;
  my @result;

  foreach my $k (sort(keys(%sfi))) {
    my $v = $sfi{$k};

    $chr = valToBlock($v, $minsfi, $maxsfi);

    # year boundary
    push @result,"|" if ($k =~ /-01$/);

    if ($v < 20.0) {
      push @result, green($chr);
    } elsif ($v < 30.0) {
      push @result, yellow($chr);
    } elsif ($v < 50.0) {
      push @result, red($chr);
    } elsif ($v < 100.0) {
      push @result, darkRed($chr);
    } else {
      push @result, blue($chr);
    }
  }
  return @result;
}

# relative to min/max
sub valToBlock {
  my $v = shift;
  my $min = shift;
  my $max = shift;
  my $step = ($max - $min)/8;
  my $chr = " ";

  if ($v <= ($min + ($step * 1))) {
    $chr = "▁";
  } elsif ($v <= ($min + ($step * 2))) {
    $chr = "▂";
  } elsif ($v <= ($min + ($step * 3))) {
    $chr = "▃";
  } elsif ($v <= ($min + ($step * 4))) {
    $chr = "▄";
  } elsif ($v <= ($min + ($step * 5))) {
    $chr = "▅";
  } elsif ($v <= ($min + ($step * 6))) {
    $chr = "▆";
  } elsif ($v <= ($min + ($step * 7))) {
    $chr = "▇";
  } elsif ($v <= ($min + ($step * 8))) {
    $chr = "▇";
  } else {
    $chr = "↑";
  }
  return $chr;
}

sub yearMarkers {
  my @vals = @_;
  my $ret = "";
  my $i = 0;
  for ($i = 0; $i < $#vals; $i++) {
    if ($vals[$i] =~ /(\d{4})-01$/) {
      $ret .= "| $1";
      $i += 4;
    } else {
      $ret .= " ";
    }
  }
  return $ret;
}

sub monToNum {
  my $mon = shift;
  my $num = -1;

  $num = "01" if uc $mon eq "JAN";
  $num = "02" if uc $mon eq "FEB";
  $num = "03" if uc $mon eq "MAR";
  $num = "04" if uc $mon eq "APR";
  $num = "05" if uc $mon eq "MAY";
  $num = "06" if uc $mon eq "JUN";
  $num = "07" if uc $mon eq "JUL";
  $num = "08" if uc $mon eq "AUG";
  $num = "09" if uc $mon eq "SEP";
  $num = "10" if uc $mon eq "OCT";
  $num = "11" if uc $mon eq "NOV";
  $num = "12" if uc $mon eq "DEC";

  return $num;
}

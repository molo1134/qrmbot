#!/usr/bin/perl
# find nearest lightning strikes, using blitzortung API
#
# 2-clause BSD license.
# Copyright (c) 2020 molo1134@github. All rights reserved.

use strict;
use utf8;
use Encode qw(decode);
use feature 'unicode_strings';
binmode(STDOUT, ":utf8");

use File::Basename;
use Cwd 'realpath';
use lib dirname(realpath(__FILE__));
use Colors;
use Location;
use Util;

use Time::Piece ();
use JSON qw( decode_json );
use POSIX qw(strftime);

@ARGV = map { decode "utf-8", $_ } @ARGV;
# eggdrop doesn't split args on spaces
@ARGV=split(' ', join(' ',@ARGV));

my $username = $ENV{'USER'} || $ENV{'USERNAME'} || getpwuid($<);
# change this to 0 to always exit with success
our $exitnonzeroonerror = 1;
$exitnonzeroonerror = 0 if $username eq getEggdropUID();

my $geo = undef;
my $query = undef;

my $i = 0;
while ($i <= $#ARGV) {
  if (!defined($geo) and $ARGV[$i] =~ /^--geo$/i) {
    $i++;
    $geo = $ARGV[$i];
    $i++;
    next;
  }
  if (defined($query)) {
    $query = $query ." ". $ARGV[$i];
  } else {
    $query = $ARGV[$i];
  }
  $i++;
}

if (not defined $query and not defined $geo) {
  if ($username eq getEggdropUID()) {
    print "usage: !blitz <grid>|<lat>,<lon>|<qth>\n";
  } else {
    print "usage: $0 <grid>|<lat>,<lon>|<qth>\n";
  }
  exit $exitnonzeroonerror;
}

my $result = argToCoords($query) if defined $query;

if (defined $query and not defined $result) {
  print "error: unable to find \"$query\"\n";
  exit $exitnonzeroonerror;
}

my ($mylat, $mylon) = split(',', $geo) if defined $geo;
($mylat, $mylon) = split(',', $result) if defined $result;

my $url = "http://bo-service.tryb.de/";
my $ua = "User-Agent: bo-android-270";
my $ct = "Content-Type: text/json";

# params: interval duration, raster base length, interval offset, region, count threshold
#my $req = '{"id":0,"method":"get_strikes_grid","params": [15,5000,0,3,0]}'; # North America only

# params: interval duration, raster base length, interval offset, count threshold
my $req = '{"id":0,"method":"get_global_strikes_grid","params":[15,5000,0,0]}';


local $/;   # read entire file -- FIXME: potentially memory hungry
#print("curl --max-time 10 -s -k -L -H \"${ua}\" -H \"${ct}\" --data '${req}' $url\n");
open (JSON, '-|', "curl --max-time 10 -s -k -L -H \"${ua}\" -H \"${ct}\" --data '${req}' $url");
my $json = <JSON>;
close(JSON);
my $j = decode_json($json) or die "parse error: $json\n";

our $xd = $j->{'xd'}; # longitude delta
our $yd = $j->{'yd'}; # latitude delta
our $x0 = $j->{'x0'}; # longitude start
our $y1 = $j->{'y1'}; # latitude start
our $xc = $j->{'xc'}; # num longitude bins
our $yc = $j->{'yc'}; # num latitude bins
our $t = $j->{'t'};   # reference timestamp
our $dt = $j->{'dt'}; # time window?

# not sure why this is necessary - should be returned by the server.
$y1 = $x0 = 0.0 if $req =~ /get_global_strikes_grid/;

my $now = time();
my $ts = Time::Piece->strptime($t, "%Y%m%dT%H:%M:%S")->epoch;
my $mindist = 20000.0;
my $closest = undef;

foreach my $r (@{$j->{'r'}}) {
  my @r = @{$r};
  my ($longIndex, $latIndex, $count, $timedelta) = @r;
  my $lat = getLatFromIndex($latIndex);
  my $lon = getLongFromIndex($longIndex);
  #my $grid = coordToGrid($lat, $lon);
  my ($dist, $bearing) = distBearing($mylat, $mylon, $lat, $lon);
  next if ($ts + $timedelta) < ($now - 15*60);
  if ($dist < $mindist) {
    $closest = "$lat,$lon,$timedelta";
    $mindist = $dist;
  }
  #printf("%s => $lat,$lon ($grid) #$count -- %.1f km, %.0f°\n", join(", ", @r), $dist, $bearing);
}

my @funny = ("bang!",
  "magic smoke came out",
  "antenna vaporized",
  "RIP your radio",
  "replace your polyphasor",
  "wish you read R56",
  "EMP detected");

if (defined $closest) {
  my ($lat, $lon, $timedelta) = split(/,/,$closest);
  #my $grid = coordToGrid($lat, $lon);
  #my $qth = geolocate($lat, $lon);
  #print "$lat, $lon: $grid $qth\n";
  my $mygrid = coordToGrid($mylat, $mylon);
  my $myqth = geolocate($mylat, $mylon);
  my ($dist, $bearing) = distBearing($mylat, $mylon, $lat, $lon);
  my $dist_mi = sprintf("%.1f", $dist * 0.62137119);
  $dist = sprintf("%.1f", $dist);
  #$bearing = sprintf("%.0f°", $bearing);
  $bearing = azToNEWS($bearing);
  my $pos;
  if ($dist < 1.0) {
    $pos = yellow("⚡⚡⚡⚡") . italic(bold($funny[int(rand($#funny))])) . italic(red(" $dist km ($dist_mi mi) $bearing"));
  } elsif ($dist < 5.0) {
    $pos = yellow("⚡⚡⚡") . italic(red("$dist km ($dist_mi mi) $bearing"));
  } elsif ($dist < 20.0) {
    $pos = yellow("⚡⚡") . red("$dist km ($dist_mi mi) $bearing");
  } elsif ($dist < 50.0) {
    $pos = yellow("⚡$dist km ($dist_mi mi) $bearing");
  } else {
    $pos = green("$dist km ($dist_mi mi) $bearing");
  }
  #printf("%s => $lat,$lon ($grid) #$count -- %.1f km, %.0f°\n", join(", ", @r), $dist $bearing);
  my $location;
  $location = "$myqth ($mygrid)" if defined $myqth;
  $location = "$mygrid" if not defined $myqth;

  printf("Nearest lightning strike to $location in last 15 minutes: $pos at %s\n", strftime("%H:%Mz", gmtime($ts + $timedelta)));
} else {
  print("No reports.  Possible server-side error.\n");
}

sub getLongFromIndex {
  my $index = shift;
  our $xd;
  our $x0;
  return $x0 + ($xd * ($index + 0.5));
}
sub getLatFromIndex {
  my $index = shift;
  our $yd;
  our $y1;
  return $y1 - ($yd * ($index + 0.5));
}


#!/usr/bin/perl -w
#
# 2-clause BSD license.
# Copyright (c) 2026 molo1134@github. All rights reserved.

# Find nearest POTA parks to a given QTH

# https://api.pota.app/park/grids/39.9/-75.5/40.7/-73.5/0

use URI::Escape;
use JSON qw( decode_json );
use strict;
use utf8;
use feature 'unicode_strings';
use Encode qw(decode);
use Math::Round;
binmode(STDOUT, ":utf8");

use File::Basename;
use Cwd 'realpath';
use lib dirname(realpath(__FILE__));
use Util;
use Colors;
use Location;

my $username = $ENV{'USER'} || $ENV{'USERNAME'} || getpwuid($<);

# change this to 0 to always exit with success
my $exitnonzeroonerror = 1;
$exitnonzeroonerror = 0 if $username eq getEggdropUID();

our $mylat = undef;
our $mylon = undef;

my $qthfile = $ENV{'HOME'} . "/.qrmbot/conf/qth";
if (-e ($qthfile)) {
  require($qthfile);
}

@ARGV = map { decode "utf-8", $_ } @ARGV;

# eggdrop doesn't split args on spaces
@ARGV=split(' ', join(' ',@ARGV));

if (($#ARGV < 0 || length($ARGV[0]) == 0) and not defined $mylat) {
  if ($username eq getEggdropUID()) {
    print "usage: !potapark <grid>|<lat>,<lon>|<qth>\n";
  } else {
    print "usage: $0 <grid>|<lat>,<lon>|<qth>\n";
  }
  exit $exitnonzeroonerror;
}

my $query = undef;
my $de = undef;

# eggdrop doesn't split args on spaces
@ARGV=split(' ', join(' ',@ARGV));
my $i = 0;
while ($i <= $#ARGV) {
  if ($ARGV[$i] =~ /^(--geo|de|from)$/i) {
    $i++;
    $de = $ARGV[$i];
    $i++;
    next;
  }
  if (defined($de) and $ARGV[$i] =~ /^--geo$/) {
    # in case we have "de XXX --geo abc,xyz"
    last;
  }
  if (defined($de)) {
    $de = $de . " " . $ARGV[$i];
    $i++;
    next;
  }
  if (defined($query)) {
    $query = $query ." ". $ARGV[$i];
  } else {
    $query = $ARGV[$i];
  }
  $i++;
}

my ($lat, $lon);

if (defined($de)) {
  my $degeo = argToCoords($de);
  ($lat, $lon) = split(',', $degeo) if defined $degeo;
}

if (defined $query) {
  my $result = argToCoords($query);
  if (!defined $result) {
    print "error: unable to find \"$query\"\n";
    exit $exitnonzeroonerror;
  }
  ($lat, $lon) = split(',', $result);
}

if (not defined $de and not defined $query and defined $mylat and defined $mylon) {
  ($lat, $lon) = ($mylat, $mylon);
}

print "me: $mylat, $mylon\n";
print "dx: $lat, $lon\n";

# bounding box: Â±0.5 degree lat/long
my ($n, $s, $e, $w) = ($lat + 0.5, $lat - 0.5, $lon + 0.5, $lon - 0.5);

my $url = "https://api.pota.app/park/grids/$s/$w/$n/$e/0";

local $/;   # read entire file -- FIXME: potentially memory hungry
open(JSON, '-|', "curl -k -L --max-time 10 --retry 1 -s '$url'");
my $json = <JSON>;
close(JSON);

my $j = decode_json($json);

my %results;

foreach my $f (@{$j->{features}}) {
  #my $ref = $f->{properties}->{reference};
  #my $name = $f->{properties}->{name};
  my $plon = $f->{geometry}->{coordinates}[0];
  my $plat = $f->{geometry}->{coordinates}[1];

  my ($dist, undef) = distBearing($lat, $lon, $plat, $plon);

  $results{$dist} = $f;
}

my $count = 0;
my $max = 10;
$max = 3 if $username eq getEggdropUID();

foreach my $d (sort { $a <=> $b } keys %results) {
  my $f = $results{$d};

  my $ref = $f->{properties}->{reference};
  my $name = $f->{properties}->{name};
  my $plon = $f->{geometry}->{coordinates}[0];
  my $plat = $f->{geometry}->{coordinates}[1];

  my ($dist, $bearing) = distBearing($lat, $lon, $plat, $plon);
  $dist = nearest(0.1, $dist);
  my $dir = azToNEWS($bearing);

  my $placename = geolocate($plat, $plon);

  print "$ref: $name";
  print " -- $placename" if defined $placename;
  print " -- $dist km $dir\n";
  ++$count;
  last if $count == $max;
}
